; serve.x -- Minimal HTTP server for turtle graphics viewer
;
; Serves turtle.html on / and segment JSON on /segments.
; Uses FFI to wrap socket syscalls.
;
; Usage:
;   (import logo/turtle)
;   (import logo/serve)
;   ; ... run Logo commands to populate segments ...
;   (turtle-serve 8080)
;   ; Open http://localhost:8080 in browser

; %lang-root is THE BUNDLE'S OWN DIRECTORY, and it comes from the platform:
; x.sh defines it after it has read lang.xon and found this tree, ahead of
; run.x.  It is a seam row (x-lang's tools/contract/seam.x, class `bundle`),
; so it is as much a promise as %repl-prompt or import-path! -- and unlike
; %install-root it needs no guard, because a bundle cannot be loaded in a tree
; where it is unbound.
;
; The linter reads one file at a time, so a name the boot arrangement provides
; looks undefined here -- the same reason float.x declares %bigint-base and
; syscall.x declares %param-os.  Declared, not defined: this file must not be
; the second place that knows where the bundle lives.
; lint-known: %lang-root

(import x/sys/posix)
(import x/sys/file)
; Socket plumbing is homed on the Socket class (#29) -- this app is its
; first consumer; the Darwin-only constants that used to live here moved
; there and grew their Linux column.
(import x/sys/socket)
; Fetch the ptr/ffi prims from the catalog (ns `ptr`/`ffi` are de-registered, R5).
(def %ptr-call (prim-ref 'ptr 'call))
(def %dlopen (prim-ref 'ffi 'dlopen))
(def %dlsym (prim-ref 'ffi 'dlsym))
; Fetch the io plumbing prims from the catalog (ns `io` partly de-registered, R5).
(def %write-to-str (prim-ref 'io 'write-to-str))


; ============================================================
; HTTP helpers
; ============================================================

; Extract the request path from an HTTP request string.
; "GET /path HTTP/1.1\r\n..." → "/path"
(def %http-path
  (fn (_ request)
    (if (null? request) "/"
      ; nested let, not def-in-do: this is the tail, so def would leak to global
      (let ((%find-space
             (fn (self i)
               (if (>= i (Str8 length request)) 0
                 (if (Char =? (Str8 ref i request) #\space) i
                   (self (+ i 1)))))))
        (let ((start (+ (%find-space 0) 1)))
          (let ((end (%find-space start)))
            (if (>= start end) "/"
              (Str8 sub start (- end start) request))))))))

; Build an HTTP response string.
(def %http-response
  (fn (_ status content-type body)
    (Str append "HTTP/1.1 " status "\r\n"
         "Content-Type: " content-type "\r\n"
         "Content-Length: " (%number->str (Str8 length body)) "\r\n"
         "Access-Control-Allow-Origin: *\r\n"
         "Connection: close\r\n"
         "\r\n"
         body)))

; ============================================================
; File reading
; ============================================================

; Whole-file reads ride (File read-all).  The HTTP-serving policy stays:
; an unreadable path answers "" (a 404's body), never an error (#229).
(def %read-or-empty
  (fn (_ path)
    (guard (_ "") (File read-all path))))

; ============================================================
; Bytecode file — flat JSON array entries, one per line
; ============================================================

(def %bc-path "/tmp/turtle.bc")
(def %bc-fd -1)
(def %fstr (fn (_ v) (%write-to-str v)))

(def %bc-open
  (fn ()
    (if (>= %bc-fd 0) ()
      (set! %bc-fd (Sys open-append %bc-path)))))

; Append one bytecode entry to the file
; 0-arg: writes "OP" + comma + newline
; 1-arg: writes "OP",val + comma + newline
; 2-arg: writes "OP",a,b + comma + newline
(def %bc-append
  (fn (_ . args)
    (%bc-open)
    (def op (first args))
    (def rest-args (rest args))
    (Sys fd-write %bc-fd
      (if (null? rest-args)
        (Str append "\"" op "\",\n")
        (if (null? (rest rest-args))
          (Str append "\"" op "\"," (%fstr (first rest-args)) ",\n")
          (Str append "\"" op "\"," (%fstr (first rest-args))
               "," (%fstr (first (rest rest-args))) ",\n"))))))

; Clear bytecode file
(def %bc-clear
  (fn ()
    (if (>= %bc-fd 0) (Sys close %bc-fd))
    (def fd (Sys open-write %bc-path))
    (if (>= fd 0) (Sys close fd))
    (set! %bc-fd (Sys open-append %bc-path))))

; Read bytecode file and wrap as JSON array
(def %bc-json
  (fn ()
    (def content (%read-or-empty %bc-path))
    (if (str=? content "") "[]"
      (Str append "[" (Str8 sub 0 (- (Str8 length content) 2) content) "]"))))

; Write initial empty bytecode file
(def %bc-write
  (fn ()
    (def fd (Sys open-write %bc-path))
    (if (>= fd 0) (Sys close fd))))

; ============================================================
; Server
; ============================================================

(def turtle-serve
  (fn (_ port)
    ; Read the HTML template.
    ;
    ; THE ONE DATA PATH IN THE BUNDLE, and the only reason %lang-root has a
    ; seam row at all: every other file here is reached by `import` or a
    ; ./-relative include-once, neither of which means "the bytes of that
    ; file".  A cwd-relative literal found the viewer only when cwd happened
    ; to be the tree root, which is to say never in an installed or pinned
    ; one -- the defect this read has now been rewritten twice to close.
    (def html-template
      (%read-or-empty (%path-join %lang-root "logo/viewer.html")))
    (if (str=? html-template "")
      (Err raise 'io "Could not read turtle.html" ()))
    ; Inject the endpoint script before </body>
    (def html-page
      (Str append "<script>window.TURTLE_ENDPOINT='/bc';</script>\n"
           html-template))
    ; Create server socket
    (def server-fd (Socket tcp-listen port))
    (display "Turtle server listening on http://localhost:" port "\n"
             "Press Ctrl+C to stop.\n")
    ; Accept loop
    (def %serve-loop
      (fn (self)
        (def client-fd (guard (_ -1) (Socket accept server-fd)))
        (if (< client-fd 0) (self)  ; Accept failed, retry
          (do
            (guard (err
                (display "Request error: ") (display err) (newline))
              ; Read request
              (def request (Socket recv client-fd 4096))
              (def path (%http-path request))
              ; Dispatch
              (def response
                (if (str=? path "/bc")
                  (%http-response "200 OK" "application/json" (%bc-json))
                  (if (str=? path "/")
                    (%http-response "200 OK" "text/html; charset=utf-8" html-page)
                    (%http-response "404 Not Found" "text/plain" "Not found"))))
              ; Send response
              (Socket send client-fd response))
            ; Close client connection
            (Socket close client-fd)
            (self)))))
    (%serve-loop)))

(provide logo/serve turtle-serve %bc-write)
